#!/bin/zsh
# shortcut to this dotfiles path is $ZSH
export ZSH="$HOME/.dotfiles"

# your code folder that we can `c [tab]` to
export CODEDIR="/mnt/Storage/Code"

# your project folder that we can `c [tab]` to
export PROJECTS="/mnt/Storage/Projects"

# your default editor
# export EDITOR='subl'
export EDITOR='code'

# all of our zsh files
typeset -U config_files
config_files=($ZSH/*/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}; do
  source "$file"
done

# auto "use" the version of node specified in .nvmrc file when changing directories?
# export NVM_AUTO_USE=true

# load antibody plugins
source ~/.antibody_bundles.txt

# load everything less the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
  source "$file"
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -Uz compinit
# this worked on osx, not in linux
# if [ $(date +'%j') != $(stat -f '%Sm' -t '%j' ~/.zcompdump) ]; then
#   compinit
# else
#   compinit -C
# fi
re_initialize=0
for match in $HOME/.zcompdump*(.Nmh+24); do
  re_initialize=1
  break
done

if [ "$re_initialize" -eq "1" ]; then
  compinit
else
  compinit -C
fi

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
  source "$file"
done

unset config_files

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
[[ -a ~/.localrc ]] && source ~/.localrc
